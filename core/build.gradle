plugins {
  id 'net.researchgate.release' version '2.3.4'
  id "de.undercouch.download" version "2.1.0"
}

apply from:'../gradle/gradle/application.java.jena.gradle'
apply from:'../gradle/gradle/gradle.distribution.gradle'

release {
    // Ignore upstream changes
    failOnUpdateNeeded = false
    // Ignore local changes
    failOnPublishNeeded = false 
    preTagCommitMessage = '[Release] Version '
    newVersionCommitMessage = '[POST-Release] Updated Version to '
    tagCommitMessage = '[Release] Tag '
}

def xesAndOpenSpexDeps = ['https://svn.win.tue.nl/trac/prom/export/latest/Releases/OpenXES/OpenXES.jar', 
     'https://svn.win.tue.nl/trac/prom/export/26781/OpenXES/tags/2.0.57/lib/Spex.jar',
     'http://opldownload.s3.amazonaws.com/uda/virtuoso/7.2/rdfproviders/jena/30/virt_jena3.jar',
     'http://opldownload.s3.amazonaws.com/uda/virtuoso/7.2/jdbc/virtjdbc4.jar']
     
def testcontainerDependencies = ['org.testcontainers:testcontainers:1.0.2', 
     'org.testcontainers:jdbc:1.0.2', 
     'org.testcontainers:virtuoso:1.0.2']

dependencies {
    compile  project(':ontmalizer')

    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'org.slf4j:slf4j-log4j12:1.7.13'

    compile 'org.camunda.bpm.model:camunda-bpmn-model:7.3.0'

    compile 'org.hashids:hashids:1.0.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    
    // Download spex and openspex if necessary
    def libFolder = file("lib") 
    if(!libFolder.exists()){
        libFolder.mkdirs()   
    }
    download {
       src xesAndOpenSpexDeps 
       dest 'lib/' 
       overwrite false
    }
    compile files('lib/OpenXES.jar', 'lib/Spex.jar', 'lib/virt_jena3.jar', 'lib/virtjdbc4.jar')
    
    // Exclude httpclient v2.3 because it results into a ClassCastException when used with the QueryFactory 
    // which depends on httpclient v2.2.x
    testcontainerDependencies.each { dependency ->
      integrationCompile(dependency){
         exclude module: 'httpclient'
      }
    }
}
