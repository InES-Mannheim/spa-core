apply plugin: 'application'

// Set main class
mainClassName = "de.unima.core.application.local.SPA"

// Create sources and javadoc
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    dependsOn sourcesJar
    dependsOn javadocJar
}

distributions {
    main {
        contents {
           // Add source and javadoc
           from('build/libs'){
              include '*sources.jar'
              include '*javadoc.jar'
              into 'docs'
           }
           // Add example project and replace placeholders with current values 
           from('example'){
              into 'example'
              filesMatching('**/build.gradle'){
	          expand(coreName: "${baseName}", coreVersion: "${version}")
              }
           }
        }
    }
}

// Exclude bin folder
distZip.eachFile{ 
   if(it.path.contains("bin")) it.exclude()
}

distTar.eachFile{ 
   if(it.path.contains("bin")) it.exclude()
}
