subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven'

    group = 'de.unima'

    javadoc {
        failOnError = false
    }

    tasks.withType(JavaCompile){
        options.encoding = 'UTF-8'
    }

    repositories {
        jcenter()
    }

    sourceSets {
        integration {
            compileClasspath += sourceSets.test.compileClasspath
            runtimeClasspath += sourceSets.test.runtimeClasspath
            resources { srcDir 'src/test/resources'}
        } 
    }

    // Add integrationCompile configration to eclipse classpath
    eclipse {
        classpath {
            plusConfigurations += [ configurations.integrationCompile ]
        }
    }

    dependencies {
        compile 'com.google.guava:guava:18.0' 
	compile 'org.slf4j:slf4j-api:1.7.13'
	compile 'org.slf4j:slf4j-log4j12:1.7.13'
	testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
    }

    task intTest(type: Test) {
        group 'Verification'
        description 'Runs the integration tests.'
        testClassesDir = sourceSets.integration.output.classesDir
        classpath = sourceSets.integration.runtimeClasspath
    }
  
    check {
        dependsOn intTest
    }

}
