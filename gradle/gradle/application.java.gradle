subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    
    // used as group id in pom
    group = 'de.unima'

    // javdoc errors do not destroy build
    javadoc {
        failOnError = false
    }

    // always assume UTF-8 encoding while compiling
    tasks.withType(JavaCompile){
        options.encoding = 'UTF-8'
    }

    repositories {
        maven {
          url "http://eris-vm109.uni-mannheim.de:8081/artifactory/spa"
        }
        jcenter()
    }

    // Add source set for integration tests
    sourceSets {
        integration {
            compileClasspath += sourceSets.test.compileClasspath
            runtimeClasspath += sourceSets.test.runtimeClasspath
            resources { srcDir 'src/test/resources'}
        } 
    }

    // Add integrationCompile configration to eclipse classpath
    eclipse {
        classpath {
            plusConfigurations += [ configurations.integrationCompile ]
        }
    }

    dependencies {
        compile 'com.google.guava:guava:18.0' 
	compile 'org.slf4j:slf4j-api:1.7.13'
	compile 'org.slf4j:slf4j-log4j12:1.7.13'
	testCompile 'junit:junit:4.12'
        testCompile 'org.hamcrest:hamcrest-all:1.3'
    }

    // execute integration tests only
    task intTest(type: Test) {
        group 'Verification'
        description 'Runs the integration tests.'
        testClassesDir = sourceSets.integration.output.classesDir
        classpath = sourceSets.integration.runtimeClasspath
    }
  
    // check should depent on integration tests
    check {
        dependsOn intTest
    }

}
